// SPDX-License-Identifier: MIT
pragma solidity ^0.6.12;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/SafeERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/math/SafeMath.sol";

contract Honeycomb is Ownable {
    using SafeMath for uint256;
    using SafeERC20 for IERC20;

    // Info of each user.
    struct UserInfo {
        uint256 amount;     // How many LP tokens the user has provided.
        uint256 rewardDebt; // Reward debt.
        uint earned;        // Earned HONEY.
    }

    // Info of each pool.
    struct PoolInfo {
        IERC20 lpToken;           // Address of LP token contract.
        uint256 allocPoint;       // How many allocation points assigned to this pool.
        uint256 lastRewardBlock;  // Last block number that HONEYs distribution occurs.
        uint256 accHoneyPerShare; // Accumulated HONEYs per share, times 1e12.
    }

    // The HONEY TOKEN!
    IERC20 public honey;
    // HONEY tokens distributed per block.
    uint256 public honeyPerBlock;

    // Info of each pool.
    PoolInfo[] public poolInfo;
    // Info of each user that stakes LP tokens.
    mapping (uint256 => mapping (address => UserInfo)) public userInfo;
    // Total allocation poitns. Must be the sum of all allocation points in all pools.
    uint256 public totalAllocPoint = 0;
    // The block number when HONEY mining starts.
    uint256 public startBlock;
    // The block number when HONEY mining ends.
    uint256 public endBlock;

    event Deposit(address indexed user, uint256 indexed pid, uint256 amount);
    event Withdraw(address indexed user, uint256 indexed pid, uint256 amount);
    event EmergencyWithdraw(address indexed user, uint256 indexed pid, uint256 amount);

    constructor(
        address _honey,
        uint256 _honeyPerBlock,
        uint256 _startBlock,
        uint256 _endBlock
    ) public {
        honey = IERC20(_honey);
        honeyPerBlock = _honeyPerBlock;
        endBlock = _endBlock;
        startBlock = _startBlock;
    }

    function poolLength() external view returns (uint256) {
        return poolInfo.length;
    }

    // Add a new lp to the pool. Can only be called by the owner.
    // XXX DO NOT add the same LP token more than once. Rewards will be messed up if you do.
    function add(uint256 _allocPoint, IERC20 _lpToken, bool _withUpdate) public onlyOwner {
        if (_withUpdate) {
            massUpdatePools();
        }
        uint256 lastRewardBlock = block.number > startBlock ? block.number : startBlock;
        totalAllocPoint = totalAllocPoint.add(_allocPoint);
        poolInfo.push(PoolInfo({
        lpToken: _lpToken,
        allocPoint: _allocPoint,
        lastRewardBlock: lastRewardBlock,
        accHoneyPerShare: 0
        }));
    }

    // Update the given pool's HONEY allocation point. Can only be called by the owner.
    function set(uint256 _pid, uint256 _allocPoint, bool _withUpdate) public onlyOwner {
        if (_withUpdate) {
            massUpdatePools();
        }
        totalAllocPoint = totalAllocPoint.sub(poolInfo[_pid].allocPoint).add(_allocPoint);
        poolInfo[_pid].allocPoint = _allocPoint;
    }

    // Return rewardable block count over the given _from to _to block.
    function getPendingBlocks(uint256 _from, uint256 _to) public view returns (uint256) {
        if (_to < startBlock) {
            return 0;
        }

        if (_to > endBlock) {
            if (_from > endBlock) {
                return 0;
            } else {
                return endBlock.sub(_from);
            }
        } else {
            return _to.sub(_from);
        }
    }

    // View function to see pending HONEYs on frontend.
    function pendingHoney(uint256 _pid, address _user) external view returns (uint256) {
        if (block.number < startBlock) {
            return 0;
        }

        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][_user];
        uint256 accHoneyPerShare = pool.accHoneyPerShare;
        uint256 lpSupply = pool.lpToken.balanceOf(address(this));
        if (block.number > pool.lastRewardBlock && lpSupply != 0) {
            uint256 multiplier = getPendingBlocks(pool.lastRewardBlock, block.number);
            uint256 honeyReward = multiplier.mul(honeyPerBlock).mul(pool.allocPoint).div(totalAllocPoint);
            accHoneyPerShare = accHoneyPerShare.add(honeyReward.mul(1e12).div(lpSupply));
        }
        return user.amount.mul(accHoneyPerShare).div(1e12).sub(user.rewardDebt);
    }

    // Update reward vairables for all pools. Be careful of gas spending!
    function massUpdatePools() public {
        uint256 length = poolInfo.length;
        for (uint256 pid = 0; pid < length; ++pid) {
            updatePool(pid);
        }
    }

    // Update reward variables of the given pool to be up-to-date.
    function updatePool(uint256 _pid) public {
        PoolInfo storage pool = poolInfo[_pid];
        if (block.number < startBlock || block.number <= pool.lastRewardBlock || pool.lastRewardBlock > endBlock) {
            return;
        }
        uint256 lpSupply = pool.lpToken.balanceOf(address(this));
        if (lpSupply == 0) {
            pool.lastRewardBlock = block.number;
            return;
        }
        uint256 multiplier = getPendingBlocks(pool.lastRewardBlock, block.number);
        uint256 honeyReward = multiplier.mul(honeyPerBlock).mul(pool.allocPoint).div(totalAllocPoint);
        pool.accHoneyPerShare = pool.accHoneyPerShare.add(honeyReward.mul(1e12).div(lpSupply));
        pool.lastRewardBlock = block.number;
    }

    // Deposit LP tokens for HONEY allocation.
    function deposit(uint256 _pid, uint256 _amount) public {
        if (block.number > endBlock) {
            return;
        }
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        updatePool(_pid);
        if (user.amount > 0) {
            uint256 pending = user.amount.mul(pool.accHoneyPerShare).div(1e12).sub(user.rewardDebt);
            if (pending > 0) {
                safeHoneyTransfer(_pid, msg.sender, pending);
            }
        }

        if (_amount > 0) {
            pool.lpToken.safeTransferFrom(address(msg.sender), address(this), _amount);
            user.amount = user.amount.add(_amount);
        }
        user.rewardDebt = user.amount.mul(pool.accHoneyPerShare).div(1e12);
        emit Deposit(msg.sender, _pid, _amount);
    }

    // Withdraw LP tokens.
    function withdraw(uint256 _pid, uint256 _amount) public {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        require(user.amount >= _amount, "withdraw: not good");
        updatePool(_pid);
        uint256 pending = user.amount.mul(pool.accHoneyPerShare).div(1e12).sub(user.rewardDebt);
        safeHoneyTransfer(_pid, msg.sender, pending);
        user.amount = user.amount.sub(_amount);
        user.rewardDebt = user.amount.mul(pool.accHoneyPerShare).div(1e12);
        pool.lpToken.safeTransfer(address(msg.sender), _amount);
        emit Withdraw(msg.sender, _pid, _amount);
    }

    // Withdraw without caring about rewards. EMERGENCY ONLY.
    function emergencyWithdraw(uint256 _pid) public {
        PoolInfo storage pool = poolInfo[_pid];
        UserInfo storage user = userInfo[_pid][msg.sender];
        pool.lpToken.safeTransfer(address(msg.sender), user.amount);
        emit EmergencyWithdraw(msg.sender, _pid, user.amount);
        user.amount = 0;
        user.rewardDebt = 0;
    }

    // Safe honey transfer function, just in case if rounding error causes pool to not have enough HONEYs.
    function safeHoneyTransfer(uint256 _pid, address _to, uint256 _amount) internal {
        uint256 honeyBal = honey.balanceOf(address(this));
        if (honeyBal == 0) {
            return;
        }
        UserInfo storage user = userInfo[_pid][_to];
        if (_amount > honeyBal) {
            honey.transfer(_to, honeyBal);
            user.earned = user.earned.add(honeyBal);
        } else {
            honey.transfer(_to, _amount);
            user.earned = user.earned.add(_amount);
        }
    }
}
